import com.moowork.gradle.node.task.NpmTask

plugins {
    id "com.moowork.node" version "0.11"
}

node {
    // Version of node to use.
    version = '5.6.0'

    // Version of npm to use.
    npmVersion = '2.14.20'

    // Base URL for fetching node distributions (change if you have a mirror).
    distBaseUrl = 'https://nodejs.org/dist'

    // If true, it will download node using above parameters.
    // If false, it will try to use globally installed node.
    download = true

    // Set the work directory where node_modules should be located
    nodeModulesDir = file("${project.projectDir}")
}

configurations {
    compile
}

task zipBuild(type: Zip, dependsOn: ["build"]) {
    from "build"
    archiveName 'distribution.zip'
    destinationDir projectDir
}

artifacts {
    compile zipBuild
}

buildDir = "build"

task clean(type: Delete) {
    delete "build"
}

task build(type: NpmTask) {
    args = ['run', 'build']
}

task run(type: NpmTask) {
    args = ['run', 'start']
}

task test(type: NpmTask) {
    args = ['test']
}

task testWatch(type: NpmTask) {
    args = ['run', 'test:w']
}

build.dependsOn npmInstall